plugins {
    id 'com.android.application'
    id 'de.mannodermaus.android-junit5'
    id 'checkstyle'
    id 'dagger.hilt.android.plugin'
}

ext {
    junitVersion = '5.8.0-M1'
    androidxTestVersion = '1.4.0'
    junitTestVersion = '1.2.2'
    retrofitVersion = '2.9.0'
    mockitoVersion = '3.11.2'
    roomVersion = '2.3.0'
    hiltVersion = '2.38.1'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "me.bramhaag.owouploader"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        // JUnit 5 Instrumentation runner
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        testInstrumentationRunnerArgument 'runnerBuilder', 'de.mannodermaus.junit5.AndroidJUnit5Builder'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "String", "SENTRY_DSN", "\"https://6e1c2049e4eb4fc7a867d59c51df6a90@o935366.ingest.sentry.io/5885112\""
        }

        debug {
            buildConfigField "String", "SENTRY_DSN", "\"\""
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    buildFeatures {
        viewBinding true
    }

    // Exclude conflicting files in test APK. See
    // https://github.com/mannodermaus/android-junit5/wiki/Instrumentation-Tests-Setup#4-setup-packaging
    packagingOptions {
        exclude "META-INF/LICENSE*"
    }
}

dependencies {
    // RxJava
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'
    implementation "io.reactivex.rxjava3:rxjava:3.0.13"

    // DI
    implementation 'androidx.hilt:hilt-work:1.0.0'
    annotationProcessor 'androidx.hilt:hilt-compiler:1.0.0'
    implementation "androidx.hilt:hilt-lifecycle-viewmodel:1.0.0-alpha03"

    implementation "com.google.dagger:hilt-android:$hiltVersion"
    annotationProcessor "com.google.dagger:hilt-compiler:$hiltVersion"

    // Room
    implementation "androidx.room:room-runtime:$roomVersion"
    annotationProcessor "androidx.room:room-compiler:$roomVersion"
    implementation "androidx.room:room-rxjava3:$roomVersion"

    // Components
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.1'
    implementation 'androidx.activity:activity:1.3.0-rc01'
    implementation 'androidx.fragment:fragment:1.3.6'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.getbase:floatingactionbutton:1.10.1'

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-scalars:$retrofitVersion"

    // Glide
    implementation 'com.github.bumptech.glide:glide:4.12.0'

    // Telemetry
    implementation 'io.sentry:sentry-android:5.1.0-beta.9'

    // Testing
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$junitVersion"
    testImplementation "org.mockito:mockito-inline:$mockitoVersion"
    testImplementation "org.mockito:mockito-junit-jupiter:$mockitoVersion"

    androidTestImplementation "androidx.test:runner:$androidxTestVersion"
    androidTestImplementation "androidx.test:core:$androidxTestVersion"
    androidTestImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    androidTestImplementation "org.mockito:mockito-android:$mockitoVersion"
    androidTestImplementation "org.mockito:mockito-junit-jupiter:$mockitoVersion"
    androidTestImplementation "de.mannodermaus.junit5:android-test-core:$junitTestVersion"
    androidTestRuntimeOnly "de.mannodermaus.junit5:android-test-runner:$junitTestVersion"

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'
}

checkstyle {
    toolVersion "8.42"
    ignoreFailures false
    maxWarnings 0
}

task checkstyle(type: Checkstyle) {
    source 'src/main'
    include '**/*.java'
    classpath = files()
}