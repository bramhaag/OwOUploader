plugins {
    id 'com.android.application'
    id 'de.mannodermaus.android-junit5'
    id 'checkstyle'
}

ext {
    junitVersion = '5.8.0-M1'
    androidxTestVersion = '1.3.0'
    junitTestVersion = '1.2.2'
    retrofitVersion = '2.9.0'
    mockitoVersion = '3.9.0'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "me.bramhaag.owouploader"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        // JUnit 5 Instrumentation runner
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        testInstrumentationRunnerArgument 'runnerBuilder', 'de.mannodermaus.junit5.AndroidJUnit5Builder'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    // Exclude conflicting files in test APK. See
    // https://github.com/mannodermaus/android-junit5/wiki/Instrumentation-Tests-Setup#4-setup-packaging
    packagingOptions {
        exclude "META-INF/LICENSE*"
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'com.getbase:floatingactionbutton:1.10.1'
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-scalars:$retrofitVersion"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$junitVersion"
    testImplementation "org.mockito:mockito-inline:$mockitoVersion"
    testImplementation "org.mockito:mockito-junit-jupiter:$mockitoVersion"

    androidTestImplementation "androidx.test:runner:$androidxTestVersion"
    androidTestImplementation "androidx.test:core:$androidxTestVersion"
    androidTestImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    androidTestImplementation "org.mockito:mockito-android:$mockitoVersion"
    androidTestImplementation "org.mockito:mockito-junit-jupiter:$mockitoVersion"
    androidTestImplementation "de.mannodermaus.junit5:android-test-core:$junitTestVersion"
    androidTestRuntimeOnly "de.mannodermaus.junit5:android-test-runner:$junitTestVersion"
}

checkstyle {
    toolVersion "8.42"
    ignoreFailures false
    maxWarnings 0
}

task checkstyle(type: Checkstyle) {
    source 'src/main'
    include '**/*.java'
    classpath = files()
}